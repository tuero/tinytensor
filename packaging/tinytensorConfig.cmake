cmake_minimum_required(VERSION 3.19)

set(tinytensor_known_comps static shared)
set(tinytensor_comp_static NO)
set(tinytensor_comp_shared NO)
foreach (tinytensor_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (tinytensor_comp IN_LIST tinytensor_known_comps)
        set(tinytensor_comp_${tinytensor_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "tinytensor does not recognize component `${tinytensor_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (tinytensor_comp_static AND tinytensor_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
        "tinytensor `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(tinytensor_static_targets "${CMAKE_CURRENT_LIST_DIR}/tinytensor-static-targets.cmake")
set(tinytensor_shared_targets "${CMAKE_CURRENT_LIST_DIR}/tinytensor-shared-targets.cmake")

macro(tinytensor_load_targets type)
    if (NOT EXISTS "${tinytensor_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "tinytensor `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${tinytensor_${type}_targets}")
endmacro()

if (tinytensor_comp_static)
    tinytensor_load_targets(static)
elseif (tinytensor_comp_shared)
    tinytensor_load_targets(shared)
elseif (DEFINED tinytensor_SHARED_LIBS AND tinytensor_SHARED_LIBS)
    tinytensor_load_targets(shared)
elseif (DEFINED tinytensor_SHARED_LIBS AND NOT tinytensor_SHARED_LIBS)
    tinytensor_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${tinytensor_shared_targets}")
        tinytensor_load_targets(shared)
    else ()
        tinytensor_load_targets(static)
    endif ()
else ()
    if (EXISTS "${tinytensor_static_targets}")
        tinytensor_load_targets(static)
    else ()
        tinytensor_load_targets(shared)
    endif ()
endif ()
